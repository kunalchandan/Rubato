plugins {
    id 'com.android.application'
}

android {
    compileSdk = 35

    defaultConfig {
        minSdkVersion = 24
        targetSdk = 35
        applicationId "one.chandan.rubato"

        versionCode = 27
        versionName = '4.0.0'

        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    buildTypes {
        release {
            shrinkResources = true
            minifyEnabled = true
            debuggable = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    testOptions {
        animationsDisabled = true
        managedDevices {
            localDevices {
                pixel2Api29 {
                    device = "Pixel 2"
                    apiLevel = 29
                    systemImageSource = "google_apis"
                    testedAbi = "x86"
                }
                pixel6Api33 {
                    device = "Pixel 6"
                    apiLevel = 33
                    systemImageSource = "google_apis"
                    testedAbi = "x86_64"
                }
                pixel7Api35 {
                    device = "Pixel 7"
                    apiLevel = 35
                    systemImageSource = "google_apis"
                    testedAbi = "x86_64"
                }
                pixelTabletApi34 {
                    device = "Pixel Tablet"
                    apiLevel = 34
                    systemImageSource = "google_apis"
                    testedAbi = "x86_64"
                }
                automotiveApi34 {
                    device = "Automotive (1024p landscape)"
                    apiLevel = 34
                    systemImageSource = "aosp"
                }
            }
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/libffmpegJNI.so']
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }

    namespace = 'one.chandan.rubato'
}

dependencies {
    def includeFfmpeg = !project.hasProperty('excludeFfmpeg')
    if (includeFfmpeg) {
        implementation files('../libs/lib-decoder-ffmpeg-release.aar')
    }
    compileOnly 'com.google.j2objc:j2objc-annotations:2.8'

    // AndroidX
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.6'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.room:room-runtime:2.8.4'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.work:work-runtime:2.9.0'

    // Android Material
    implementation 'com.google.android.material:material:1.10.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:annotations:4.16.0'

    // Media3
    implementation 'androidx.media3:media3-session:1.5.1'
    implementation 'androidx.media3:media3-common:1.5.1'
    implementation 'androidx.media3:media3-exoplayer:1.5.1'
    implementation 'androidx.media3:media3-ui:1.5.1'
    implementation 'androidx.media3:media3-exoplayer-hls:1.5.1'
    implementation 'androidx.media3:media3-cast:1.5.1'

    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    annotationProcessor 'androidx.room:room-compiler:2.8.4'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.14'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    debugImplementation 'androidx.fragment:fragment-testing:1.8.6'
    implementation 'org.jetbrains.kotlin:kotlin-parcelize-runtime:2.2.10'
    kotlinCompilerPluginClasspath 'org.jetbrains.kotlin:kotlin-parcelize-compiler:2.2.10'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-Xlint:-options']
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions.freeCompilerArgs.addAll(
            providers.provider {
                configurations.kotlinCompilerPluginClasspath.files.findAll {
                    it.name.contains("kotlin-parcelize-compiler")
                }.collect { file -> "-Xplugin=${file.absolutePath}" }
            }
    )
}

tasks.register("generateMockups") {
    group = "release"
    description = "Capture mockup screenshots via adb (prompts for each screen)."
    doLast {
        def script = new File(rootDir, "tools/generate_mockups.py").absolutePath
        def serial = System.getenv("ADB_SERIAL")
        def cmd = ["python", script, "--set", "all"]
        if (serial != null && !serial.trim().isEmpty()) {
            cmd += ["--serial", serial.trim()]
        }
        exec {
            commandLine cmd
        }
    }
}

tasks.register("releaseWithMockups") {
    group = "release"
    description = "Generate mockup screenshots then assemble release."
    dependsOn "generateMockups", "assembleRelease"
}
